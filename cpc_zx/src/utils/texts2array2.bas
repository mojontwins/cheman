' texts2array.bas
' Copyleft 2016 the Mojon Twins

Sub usage 
	Print "Usage:"
	Print "$ texts2array.exe in.txt out.h label pad"
End Sub

Dim As Integer fIn, fOut, pad
Dim As String myLine
Dim As Integer i, firstTime, order

pad = Val (Command (4))
If pad = 0 Then usage: End

fIn = FreeFile: Open Command (1) For Input As #fIn
fOut = FreeFile: Open Command (2) For Output As #fOut

Print #fOut, "// Text array generated by texts2array.exe"
Print #fOut, "// Copyleft 2016 by The Mojon Twins"
Print #fOut, ""
Print #fOut, "// Padded to " & pad & " chars per line."
Print #fOut, ""
Print #fOut, "const unsigned char " & Command (3) & " [] = {"

firstTime = -1: order = 0
While Not Eof (fIn)
	If firstTime Then firstTime = 0 Else Print #fOut, ",	// " & Hex (order, 2) & " : " & myLine: order = order + 1
	Line Input #fIn, myLine
	For i = 1 To pad
		if i <= Len (myLine) Then
			Print #fOut, "0x" & Lcase (Hex (Asc (Mid (myLine, i, 1)), 2));
		Else
			Print #fOut, "0x20";
		End If
		if i < pad Then Print #fOut, ", ";
	Next i
Wend

Print #fOut, " 	// " & Hex (order, 2) & " : " & myLine
Print #fOut, "};"

Close #fIn, #fOut
